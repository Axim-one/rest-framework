plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
}

ext {
    mybatisStarterVersion = '3.0.3'
}

allprojects {
    group = 'one.axim.framework'
    version = '1.1.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = '17'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        // Database

        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }
}

// ──────────────────────────────────────────
// JitPack publish (library modules only)
// ──────────────────────────────────────────
configure(subprojects.findAll { it.name != 'demo' }) {
    apply plugin: 'maven-publish'

    jar {
        archiveClassifier = ''
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                groupId = project.group
                artifactId = project.name
                version = project.version

                versionMapping {
                    usage('java-api') { fromResolutionOf('runtimeClasspath') }
                    usage('java-runtime') { fromResolutionResult() }
                }
            }
        }
    }
}